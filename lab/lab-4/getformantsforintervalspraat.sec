

numberOfSelectedSounds = numberOfSelected ("Sound")
numberOfSelectedTextGrids = numberOfSelected ("TextGrid")

if numberOfSelectedSounds != 1 or numberOfSelectedTextGrids != 1
  exitScript: "Please select exactly one Sound and exactly one TextGrid!"
endif

sound = selected("Sound",1)
textgrid = selected("TextGrid",1)

# prompt user for phonemes to  measure
form Get formants
   sentence Extract_formants_for_labels_matching ^(ix|eh|iy|uw|ao|er|ah|uh|ae|aa)$
   natural Number_of_phoneme_tier 1
   choice Gender: 5
       button Female
       button Male
endform

# shorten regexp variable
  labelexp$ = extract_formants_for_labels_matching$

# shorten tiernumber variable
tier = number_of_phoneme_tier

# clear info window
writeInfoLine: "label time F1 F2 F3"

# prepare plot 
Erase all
12
Select outer viewport: 0, 6, 0, 4
Draw inner box
18
Text top: "yes", "Formant plot"
12
Axes: 2900, 600, 800, 200
14
Text bottom: "yes", "F2"
Text left: "yes", "F1"
12
Marks bottom every: 1, 500, "yes", "yes", "yes"
Marks left every: 1, 100, "yes", "yes", "yes"


# extract sounds corresponding to desired phones
selectObject: sound, textgrid
Extract intervals where: tier, "yes", "matches (regex)", labelexp$
# save IDs of all selected sounds in an array so we can delete them later
# get number of selected objects
n = numberOfSelected ()
for i to n
    # this gets the ID of the i-th selected object
    sound[i] = selected (i) 
endfor

# convert all extracted sounds to formants
if gender>1
   maxval = 5000
else
   maxval = 5500
endif
To Formant (burg): 0, 5, maxval, 0.025, 50

# get number of selected objects
n = numberOfSelected ()
# save IDs of all selected formant objects in an array
for i to n
    # this gets the ID of the i-th selected object
    formant[i] = selected (i) 
endfor

# now iterate to extract formants
for i to n
    # select formant object
    selectObject: formant[i]
    # find middle time
    start = Get start time
    dur = Get total duration
    sampletime = start + 0.5*dur
    f1 = Get value at time: 1, sampletime, "Hertz", "Linear"
    f2 = Get value at time: 2, sampletime, "Hertz", "Linear"
    f3 = Get value at time: 3, sampletime, "Hertz", "Linear"
    # find out which vowel this was
    selectObject: textgrid
    interval = Get interval at time: tier, sampletime
    label$ = Get label of interval: tier, interval
    appendInfoLine: label$, " ", fixed$(sampletime,2), " ",fixed$( f1,0), " ", fixed$(f2,0), " ", fixed$(f3,0)
    # plot this data point
    Text: f2, "Centre", f1, "Half", label$
endfor

   
# clean up
selectObject: formant[1],sound[1]
for i from 2 to n
    plusObject: formant[i], sound[i]
endfor
Remove

selectObject: sound, textgrid
